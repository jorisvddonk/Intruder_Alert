class ParallaxStar : Actor {
  Vector3 initialPosition;
  float posMultiplyFactor;
  Spaceship playerShip;

  Default {
    +NOGRAVITY;
    +WALLSPRITE;
    +DONTINTERPOLATE;
  }

  States {
    Spawn:
      goto Star1;
    Star1:
      SPAC B -1;
      loop;
    Star2:
      SPAC C -1;
      loop;
  }

  override void PostBeginPlay()
  {
    initialPosition = (self.Pos.x, self.Pos.y, self.Pos.z);
    setXYZ((self.Pos.x, self.Pos.y + Random(-100, 100), self.Pos.z + Random(-100, 100)));
  }

  void setPlayerShip(Spaceship tgt) {
    self.playerShip = tgt;
  }

  void setposMultiplyFactor(float f) {
    self.posMultiplyFactor = f;
    if (f < 1) {
      SetState(ResolveState("Star2"));
    }
  }

  virtual Vector3 getNewDoomWorldPosition() {
    float newX = self.pos.x;
    float newY = self.pos.y - self.playerShip.mov.x * 0.01 * self.posMultiplyFactor;
    float newZ = self.pos.z - self.playerShip.mov.y * 0.01 * self.posMultiplyFactor;
    if (newY - self.initialPosition.y < -100) {
      newY = newY + 200;
    }
    if (newY - self.initialPosition.y > 100) {
      newY = newY - 200;
    }
    if (newZ - self.initialPosition.z < -100) {
      newZ = newZ + 200;
    }
    if (newZ - self.initialPosition.z > 100) {
      newZ = newZ - 200;
    }
    return (newX, newY, newZ);
  }

  override void Tick() {
    SetXYZ(getNewDoomWorldPosition());
  }
}
